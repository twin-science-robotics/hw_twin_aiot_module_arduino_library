/*
  IFTTT ile SÄ±caklÄ±k AlarmÄ±
  
  Bu Ã¶rnek Twin AIoT kartÄ± ile sÄ±caklÄ±k ve nem sensÃ¶rÃ¼nÃ¼ kullanarak belirlediÄŸiniz alarm durumunda
  IFTTT (If This Then That) servisine veri gÃ¶nderir. Belirli sÄ±caklÄ±k veya nem 
  deÄŸerlerine ulaÅŸÄ±ldÄ±ÄŸÄ±nda IFTTT mobil uygulamasÄ± Ã¼zerinden bildirim alÄ±rsÄ±nÄ±z.

  YAPILACAKLAR:
  1. IFTTT hesabÄ± oluÅŸturun ve giriÅŸ yapÄ±n. (https://ifttt.com)
  2. "Create" butonuna tÄ±klayÄ±n
  3. "If This" kÄ±smÄ±nda "Webhooks" servisini seÃ§in. Webhooks servisi iÃ§in pro hesabÄ± gerektirmektedir. 7 gÃ¼nlÃ¼k Ã¼cretsiz kullanabilirsiniz.
  4. "Receive a web request" seÃ§in ve Connect' e basÄ±n.
  5. Event Name: "sicaklik_alarm" yazÄ±n ve Create trigger'a basÄ±n. (Kodla aynÄ± olmalÄ±)
  6. "Then That" kÄ±smÄ±nda Notifications eylemini seÃ§in.
  7. "Send a notification from the IFTTT app" seÃ§in. Connect'e basÄ±n.
  8. Message bÃ¶lÃ¼mÃ¼ne aÅŸaÄŸÄ±daki ifadeyi yapÄ±ÅŸtÄ±rabilirsiniz veya istediÄŸiniz ÅŸekilde dÃ¼zenleyebilirsiniz:
  
  âš ï¸ Ortam AlarmÄ±! ğŸŒ¡ï¸
  SÄ±caklÄ±k: {{Value1}}Â°C
  Nem: {{Value2}}%
  LÃ¼tfen ortamÄ± kontrol edin.

  9. Create action butonuna tÄ±klayÄ±n.
  10. Continue butonuna tÄ±klayÄ±n ardÄ±ndan finish'e basÄ±n.
  11. OluÅŸturduÄŸunuz eventin altÄ±nda webhook url'niz gÃ¶sterilecektir. Bu urlyi kopyalayÄ±n kodda ilgili yere yepÄ±ÅŸtÄ±rÄ±n.
  12. IFTTT mobil uygulamasÄ±nÄ± indirin ve hesabÄ±nÄ±zla giriÅŸ yapÄ±n.
  13. Kodu yÃ¼klediÄŸinizde 60 saniyede bir kontrol edilip telefonunuza bildirim gÃ¶nderilecektir.

  Not: SÄ±caklÄ±k nem sensÃ¶rÃ¼nÃ¼ coding modda olmasÄ± gerekmektedir. AyrÄ±ca Twin AIoT kartÄ±nÄ±zdan seÃ§tiÄŸiniz giriÅŸ iÃ§in
  kodda ilgili pinin "DHT_PIN" ayarlamasÄ±nÄ± yapÄ±n. D4 iÃ§in 8, D8 iÃ§in 11, D12 iÃ§in 9 yazÄ±n.

  Not : Serial Monitor'Ã¼n aktif olmasÄ± iÃ§in Tools sekmesinden "USB CDC on Boot" seÃ§eneÄŸini Enabled olarak seÃ§melisiniz.
  
  https://github.com/twin-science-robotics/hw_twin_aiot_module_arduino_library

  2 EylÃ¼l 2025
  Twin Science & Robotics
*/

#include <WiFi.h>           // Wifi101 kÃ¼tÃ¼phanesini ekleyin, yÃ¼klÃ¼ deÄŸilse KÃ¼tÃ¼phane YÃ¶neticisi'nden yÃ¼kleyin.
#include <HTTPClient.h>     // ArduinoHttpClient kÃ¼tÃ¼phanesini ekleyin, yÃ¼klÃ¼ deÄŸilse KÃ¼tÃ¼phane YÃ¶neticisi'nden yÃ¼kleyin.
#include "DHT11.h"          // Herhangi bir kÃ¼tÃ¼phane indirmenize gerek yok.

#define DHT_PIN 8           // D4 iÃ§in 8, D8 iÃ§in 11, D12 iÃ§in 9 yazÄ±n.

// Mux pinleri
const int s1_pini = 37;
const int s2_pini = 38;
const int s3_pini = 14;

const char* ssid = "wifi_isminiz";      // "wifi_isminiz" yazan yere wifi adÄ±nÄ±zÄ± giriniz.
const char* password = "wifi_sifreniz"; // "wifi_sifreniz" yazan yere wifi ÅŸifrenizi giriniz.

String url = "sizin_urlniz";  // sizin_urlniz IFTTT Webhook url'nizi buraya yapÄ±ÅŸtÄ±rÄ±n.

DHT11 dht11(DHT_PIN); // DHT sensÃ¶r nesnesi oluÅŸtur

// Zaman deÄŸiÅŸkenleri
unsigned long onceki_zaman = 0;
const unsigned long bekleme_suresi = 60000; // 1 dakika (60000 ms)

// Alarm sÄ±nÄ±r deÄŸerleri
const float max_sicaklik = 25.0;    // SÄ±caklÄ±k Limiti (Â°C)
const float max_nem = 50.0;         // Nem Limiti (%)

// Dijital giriÅŸi seÃ§imi
void digitalPinSecimi(int secilen_pin) {
  // Ã–nce tÃ¼m s pinlerini LOW yaparak resetleyelim
  pinMode(s1_pini, OUTPUT);
  pinMode(s2_pini, OUTPUT);
  pinMode(s3_pini, OUTPUT);
  digitalWrite(s1_pini, LOW);
  digitalWrite(s2_pini, LOW);
  digitalWrite(s3_pini, LOW);

  // SeÃ§ilen pine gÃ¶re doÄŸru s pinini HIGH yap
  switch (secilen_pin) {
    case 8: // D4
      digitalWrite(s1_pini, HIGH);
      break;
    case 11: // D8
      digitalWrite(s2_pini, HIGH);
      break;
    case 9: // D12
      digitalWrite(s3_pini, HIGH);
      break;
  }
}

void setup() {
  Serial.begin(115200); // Seri haberleÅŸmeyi baÅŸlat
  Serial.println("\nIFTTT ile SÄ±caklÄ±k Kontrol");

  pinMode(DHT_PIN, INPUT); // DHT pinini input olarak ayarlÄ±yoruz.
  digitalPinSecimi(DHT_PIN);

  WiFi.begin(ssid, password); // WiFi baÄŸlantÄ±sÄ±nÄ± baÅŸlat

  // WiFi baÄŸlantÄ±sÄ± kurulana kadar bekle
  int deneme_sayisi = 0;
  while (WiFi.status() != WL_CONNECTED) {
    delay(1000);
    Serial.print(".");
    deneme_sayisi++;
    if (deneme_sayisi > 30) {
      Serial.println("\nWiFi baglantisi kurulamadi!");
      break;
    }
  }
}

void loop() {
  olcumYapVeKontrolEt();
}

// SensÃ¶rden veri okuma, kontrol etme ve alarm gÃ¶nderme iÅŸlemlerini yapan fonksiyon
void olcumYapVeKontrolEt() {
  unsigned long simdiki_zaman = millis(); // Mevcut zamanÄ± al
  
  // Belirlenen bekleme sÃ¼resi dolunca Ã¶lÃ§Ã¼m yap
  if (simdiki_zaman - onceki_zaman >= bekleme_suresi) {
    onceki_zaman = simdiki_zaman;
    
    // DHT11'den sÄ±caklÄ±k ve nem oku
    float sicaklik = dht11.readTemperature();  // Celsius cinsinden
    float nem = dht11.readHumidity();          // YÃ¼zde cinsinden
    
    // Ã–lÃ§Ã¼m sonuÃ§larÄ±nÄ± seri port'a yazdÄ±r
    Serial.println("--------------------");
    Serial.print("SÄ±caklÄ±k: ");
    Serial.print(sicaklik);
    Serial.println(" Â°C");
    Serial.print("Nem: ");
    Serial.print(nem);
    Serial.println(" %");
    
    // Alarm durumunu kontrol et
    if (sicaklik > max_sicaklik || nem > max_nem) {
      Serial.println("ALARM: Sinir degerler asildi! IFTTT'ye bildiriliyor...");
      ifttt_gonder(sicaklik, nem);
    } else {
      Serial.println("Degerler normal sinirlar icerisinde.");
    }
  }
}

// IFTTT'ye HTTP POST isteÄŸi gÃ¶nderen fonksiyon
void ifttt_gonder(float sicaklik, float nem) {
  // WiFi baÄŸlantÄ±sÄ±nÄ± kontrol et
  if (WiFi.status() == WL_CONNECTED) {
    HTTPClient http;
    
    http.begin(url);
    http.addHeader("Content-Type", "application/json");
    
    // JSON veri formatÄ± oluÅŸtur
    String json_data = "{";
    json_data += "\"value1\":\"" + String(sicaklik) + "\",";
    json_data += "\"value2\":\"" + String(nem) + "\"";
    json_data += "}";
    
    http.POST(json_data);   // HTTP POST isteÄŸi gÃ¶nder
    http.end();             // HTTP baÄŸlantÄ±sÄ±nÄ± kapat
    
  } else {
    Serial.println("WiFi baÄŸlantÄ±sÄ± yok! IFTTT'ye mesaj gÃ¶nderilemedi.");
  }
}
